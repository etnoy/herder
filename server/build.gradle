plugins {
    id 'java'
    id 'org.springframework.boot' version '2.6.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'eclipse'
    id 'jacoco'
    id "io.freefair.lombok" version "6.3.0"
    id "org.sonarqube" version "3.3"
    id "com.diffplug.spotless" version "6.1.2"
}

group = 'org.owasp.herder'
version = '0.0.1'
description = 'Herder'
sourceCompatibility = '11'

// Require a log4j version not vulnerable to log4shell
ext['log4j2.version'] = '2.17.1'

repositories {
    mavenCentral()
    mavenLocal()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output

        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'org.springframework.data:spring-data-commons'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.r2dbc:r2dbc-h2'
    implementation 'com.github.spotbugs:spotbugs:4.0.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.12'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'net.sourceforge.htmlunit:htmlunit:2.39.0'
    implementation 'org.passay:passay:1.0'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'

    implementation 'dev.miku:r2dbc-mysql'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
    implementation 'com.fasterxml.jackson:jackson-bom:2.10.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.3'
    runtimeOnly 'mysql:mysql-connector-java'
    runtimeOnly 'org.hsqldb:hsqldb:2.5.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.1'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.1'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation "org.projectlombok:lombok"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.8.2'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.apache.commons:commons-lang3'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.5.13'

    integrationTestCompileOnly 'org.projectlombok:lombok'

    integrationTestAnnotationProcessor "org.projectlombok:lombok"
    integrationTestImplementation "org.projectlombok:lombok"

    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation 'org.springframework.security:spring-security-test'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api'
    integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    integrationTestImplementation 'nl.jqno.equalsverifier:equalsverifier:3.4.2'
    integrationTestImplementation 'io.projectreactor:reactor-test:3.4.14'
    integrationTestImplementation 'org.apache.commons:commons-lang3:3.11'
}



configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationTestCompile.extendsFrom testCompile
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    outputs.upToDateWhen { false }

    shouldRunAfter test
}

integrationTest {
    useJUnitPlatform()

    finalizedBy jacocoTestReport

    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

test {
    useJUnitPlatform()

    finalizedBy jacocoTestReport

    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property "sonar.projectName", "Herder"
        property "sonar.projectKey", "etnoy_herder"
        property "sonar.organization", "etnoy"
        properties["sonar.tests"] += "src/integration-test/java"
    }
}

spotless {
  ratchetFrom 'origin/master' // only format files which have changed since origin/main

  format 'misc', {
        target '**/.gitignore', '**/*.gradle', '**/*.md', '**/*.sh'
    trimTrailingWhitespace()
    indentWithSpaces()
    endWithNewline()
  }
  java {
    trimTrailingWhitespace()
    indentWithSpaces()
    endWithNewline()
    removeUnusedImports()

    googleJavaFormat()

    licenseHeaderFile "${project.rootDir}/license.template"
  }
}
